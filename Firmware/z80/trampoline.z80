	page 0
	cpu z80
;
;	SuperMEZ80-CPM Trampoline at the zero page
;
;	Copyright (C) 2023 by Hanyazou
;
; Permission is hereby granted, free of charge, to any person
; obtaining a copy of this software and associated documentation
; files (the "Software"), to deal in the Software without
; restriction, including without limitation the rights to use,
; copy, modify, merge, publish, distribute, sublicense, and/or sell
; copies of the Software, and to permit persons to whom the
; Software is furnished to do so, subject to the following
; conditions:
;
; The above copyright notice and this permission notice shall be
; included in all copies or substantial portions of the Software.
;
; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
; EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
; OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
; NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
; HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
; WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
; FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
; OTHER DEALINGS IN THE SOFTWARE.

;
;	I/O ports
;
CONDAT		EQU	1	; console output port
MON_CLEANUP	EQU	170	; clean up monitor mode
MON_PREPARE	EQU	171	; prepare monitor mode
MON_ENTER	EQU	172	; clean up monitor mode

;
;	start of zero page
;
;	Note: The condition bits are not affected by OUT, NOP, JP and JR instructions
;
	ORG	0		; reset vector
CLEANUP_START:
TGTINV_GATE:
	OUT (MON_ENTER),A
	JR EXIT
RESTORE_SP:			; destination of the return instruction (RETI or RETN)
	LD SP,0000h		; restore original SP

        DB	8 - $ dup(0)	; NOP

	ORG	0008H		; RST 08H interrupt vector
CLEANUP_IO_ADDR EQU $ + 1
	OUT (MON_PREPARE),A
	NOP
JUMP_INST:
	JP ENTRY_RST		; Restore return address by jump instruction
CLEANUP_END:

;
;	program entry
;
ENTRY_RST:
	LD	(SAVED_SP),SP
	LD	SP,INT_STACK
	PUSH	AF
	XOR	A

ENTRY:
	LD	(NMI_FLAG),A
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	PUSH	IY

	LD	SP,(SAVED_SP)	; Load original SP
	POP	HL		; Save return address
	LD	(SAVED_PC),HL

	JR	TGTINV_GATE

EXIT:
;	setup clean up code
	LD	HL,(CLEANUP_CODE_LOCATION)
	LD	A,L
	OR	A,H
	JR	Z,UPDATE_CLEANUP_CODE

	LD	DE,CLEANUP_START
	LD	B,CLEANUP_END - CLEANUP_START
COPY_CLEANUP_CODE:
	LD	A,(DE)
	LD	(HL),A
	INC	HL
	INC	DE
	DJNZ	COPY_CLEANUP_CODE

	LD	HL,(CLEANUP_CODE_LOCATION)
	LD	DE,RESTORE_SP
	ADD	HL,DE
	LD	(FAKE_RET_ADDR),HL
	JR	UPDATE_CLEANUP_CODE

;
;	NMI interrupt vector
;
	DB	66H - $ dup(0)	; NOP

	ORG	0066H		; NMI interrupt vector

	OUT	(MON_PREPARE),A	; NMI trap
	NOP
	LD	(SAVED_SP),SP
	LD	SP,INT_STACK
	PUSH	AF
	LD	A,1
	JR	ENTRY

UPDATE_CLEANUP_CODE:
	LD	IX,(CLEANUP_CODE_LOCATION)
	LD	A,MON_CLEANUP
	LD	(IX+CLEANUP_IO_ADDR),A
	LD	HL,(SAVED_SP)	; Set the origial SP
	LD	(IX+RESTORE_SP+1),L
	LD	(IX+RESTORE_SP+2),H
	LD	A,(RET_INST_NORMAL)
	LD	(IX+JUMP_INST),A

	; update return address
	INC	HL
	INC	HL
	LD	SP,HL
	LD	HL,(SAVED_PC)
	PUSH	HL

	; restore registers
	LD	SP,SAVED_IY
	POP	IY
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	EI
RET_INST_NORMAL:
	RET			; return to RESTORE_SP because SP is pointing to FAKE_RET_ADDR

;
;	work area
;	the structure of this area must be identical to struct trampoline_work_s in monitor.c
SAVED_IY:
	DEFW	0
SAVED_IX:
	DEFW	0
SAVED_HL:
	DEFW	0
SAVED_DE:
	DEFW	0
SAVED_BC:
	DEFW	0
SAVED_AF:
	DEFW	0
INT_STACK:
FAKE_RET_ADDR:
	DEFW	RESTORE_SP
SAVED_PC:
	DEFW	0
SAVED_SP:
	DEFW	0
CLEANUP_CODE_LOCATION:
	DEFW	0
NMI_FLAG:
	DEFB	0
WORK_RFU:
	DEFB	0

	END
